#version 460
#extension GL_EXT_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D outputImage;
layout(binding = 2, set = 0) uniform UniformBufferObject {
    mat4 view;
    mat4 proj;
    vec4 lightPos;
    vec4 viewPos;
    vec4 lightColor;
    mat4 lightSpaceMatrix;
    mat4 cascadeViewProj[4];
    vec4 cascadeSplits;
    vec4 shadowParams;
} ubo;

layout(location = 0) rayPayloadEXT vec3 hitValue;

void main() {
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    
    // NDC coordinates
    vec2 d = inUV * 2.0 - 1.0;
    
    // Compute ray direction from camera
    mat4 invProj = inverse(ubo.proj);
    mat4 invView = inverse(ubo.view);
    
    vec4 target = invProj * vec4(d.x, d.y, 1, 1);
    vec4 direction = invView * vec4(normalize(target.xyz), 0);
    
    vec3 origin = ubo.viewPos.xyz;
    vec3 rayDir = normalize(direction.xyz);
    
    float tMin = 0.001;
    float tMax = 10000.0;
    
    traceRayEXT(topLevelAS,      // acceleration structure
                gl_RayFlagsOpaqueEXT, // rayFlags
                0xFF,                 // cullMask
                0,                    // sbtRecordOffset
                0,                    // sbtRecordStride
                0,                    // missIndex
                origin,               // ray origin
                tMin,                 // ray min range
                rayDir,               // ray direction
                tMax,                 // ray max range
                0                     // payload location
    );
    
    imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
}
